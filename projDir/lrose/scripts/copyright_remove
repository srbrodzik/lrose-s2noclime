#! /usr/bin/perl
# *=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=* 
# ** Copyright UCAR (c) 1992 - 2001 
# ** University Corporation for Atmospheric Research(UCAR) 
# ** National Center for Atmospheric Research(NCAR) 
# ** Research Applications Program(RAP) 
# ** P.O.Box 3000, Boulder, Colorado, 80307-3000, USA 
# ** 2001/11/20 0:2:45 
# *=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=* 
#
# Function: Script to remove copyright notice from source code files.
#           The copyright is found using the special character strings
#           placed around the copyright notice by copyright_prepend.
#
# Usage:    copyright_remove target_files
#
#==============================================================================
#
# Externals
#

use Getopt::Std qw( getopts );

# Globals

$Start_pattern="*=*=*=*=*=*=*=";            # Pattern to match copyright_prepend
$End_pattern=$Start_pattern;                # Pattern to match copyright_prepend

($prog = $0) =~ s%.*/%%;                    # Program name

# Set command line defaults

$Debug=0;
$Debug_level=0;
$Cleanup=1;
$Test=0;

# Parse input parameters

&getopts('dhntk:');

if ($opt_h) {
    print STDERR "Usage: $prog [-dhnt] target-files\n";
    print STDERR " Purpose is to go through the list of target-files and remove the\n";
    print STDERR " copyright notice from the top of each file. Requires the special\n";
    print STDERR " characters placed around the copyright notice by copyright_prepend\n";
    print STDERR " on each file.\n";
    print STDERR "\t-d:\tprint debug messages\n";
    print STDERR "\t-h:\tprint this usage message\n";
    print STDERR "\t-n:\tdo not cleanup the backup tmp files\n";
    print STDERR "\t-t:\t(test mode) do not actually modify the files\n";
    print STDERR "\t-k <chars>:\tUse this keystring when searching for copyright notices\n";
    exit 0;
}

if ($opt_d) {
    $Debug = 1;
    $Debug_level =1;
}

if ($opt_n) {
    $Cleanup=0;
}

if ($opt_t) {
    $Test=1;
}

if ($opt_k) {
    $Start_pattern=$opt_k;
    $End_pattern=$Start_pattern;
} 


@ARGV >= 1    || die "Usage: $prog [-hdtn] target-files\n";

#
# loop through all the args as filenames
#

 FILE:
foreach $filename (@ARGV) {

    # Skip the filename if it is not a file

    if (!-f $filename) {
	print(STDERR "Skipping $filename - not a file\n");
	next FILE;
    }

    # Skip the filename if it is a CVS file

    if ($filename =~ /CVS/) {
	print(STDERR "Skipping $filename - is a CVS file\n");
	next FILE;
    }

    # Skip the filename if it is a binary file

    if (-B $filename) {
	print(STDERR "Skipping $filename - is a binary file\n");
	next FILE;
    }

    # Print info

    print(STDERR "Removing copyright from $filename\n");

    # Open the input and output files

    $outfile = $filename . ".modified-$prog";

    if (!open(SRCFILE, $filename)) {
	print(STDERR "Cannot open source file $filename - continuing ... \n");
	next FILE;
    }

    if (!open(OUTFILE, ">$outfile")) {
	print(STDERR "Cannot open output file $outfile - continuing ... \n");
	next FILE;
    }

    # Initialize for each file before reading it line by line

    $linenum = 0;
    $in_copyright = 0;
    $found_copyright_start = 0;
    $found_copyright_end = 0;

    # loop through the lines in the file

    while ($line = <SRCFILE>) {

	##$linenum++;

	# If not in copyright, look for start of copyright. If inside
	# the copyright, do not print the lines to the output file.
	# If outside the copyright, print the lines to the output file.

	if (!$in_copyright) {

	    # If find the start of the copyright, set a flag, 
	    # else print the line to the output file.

	    $string_loc=index($line, $Start_pattern);
	    if ($string_loc >= 0) {
		$in_copyright = 1;
		$found_copyright_start = 1;
	    } else {
		print (OUTFILE $line);
	    }

	} else {

	    # Look for the end of the copyright

	    $string_loc=index($line, $End_pattern);
	    if ($string_loc >= 0) {
		$in_copyright = 0;
		$found_copyright_end=1;
	    } 
	}

    } # while 

    # Close the files

    close(SRCFILE);
    close(OUTFILE);

    # If successful, copy the output file to the source file name

    $backup_file=$filename . ".bak-$prog";
    if (($found_copyright_start) && ($found_copyright_end)) {

	if (!$Test) {
	    system("/bin/cp $filename $backup_file");
	    system("/bin/cp $outfile $filename");
	}

    } else {
	print (STDERR "Warning: in file $filename - copyright not removed\n");
    }
    
    # Cleanup 
    
    if ($Cleanup) {
	unlink($backup_file);
	unlink($outfile);
    }

} # foreach

# Done

exit 0;
